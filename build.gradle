buildscript {

    ext {
        kotlinVersion = '1.7.10'
        springBootVersion = '2.7.1'
        logstashLogbackVersion = '7.2'
        tokenSupportVersion = '1.3.9'
        gcpStorageVersion = "2.8.0"
    }

    repositories {
        mavenCentral()

        maven {
            url "https://maven.pkg.github.com/navikt/maven-release"
            credentials {
                username = "token"
                password = "$System.env.GITHUB_TOKEN"
            }
        }
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "${kotlinVersion}"
    id "org.jetbrains.kotlin.plugin.allopen" version "${kotlinVersion}"
    id "org.jetbrains.kotlin.plugin.spring" version "$kotlinVersion"
    id 'org.springframework.boot' version "${springBootVersion}"
    id "io.spring.dependency-management" version "1.0.12.RELEASE"
    id "org.openrepose.gradle.plugins.jaxb" version "2.5.0"
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id "org.owasp.dependencycheck" version "7.1.1"
    id 'com.adarshr.test-logger' version '3.2.0'
    id "org.sonarqube" version "3.4.0.2513"
    id 'jacoco'
}

group = 'no.nav.eessi.pensjon'
version = '0.0.1-SNAPSHOT'

configurations {
    xsd
}

dependencies {

    // Spring-boot BOM-fil spesifiserer versjoner for Spring Boot-avhengigheter
    implementation(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-aop")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-actuator")

    // Spring Framework BOM-fil overstyrer så vi får siste patch-versjon av Spring Framework
    implementation(platform("org.springframework:spring-framework-bom:5.3.21"))

    implementation('org.springframework.retry:spring-retry:1.3.3')

    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:$kotlinVersion"))
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    implementation 'org.springframework.kafka:spring-kafka:2.8.5'

    //Auth
    implementation("no.nav.security:token-client-spring:${tokenSupportVersion}")
    implementation("no.nav.security:token-validation-spring:${tokenSupportVersion}")
    implementation("no.nav.security:token-validation-jaxrs:${tokenSupportVersion}")
    implementation("no.nav.security:token-validation-test-support:${tokenSupportVersion}")

    // Logging
    implementation("net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}")
    configurations { all*.exclude group: 'commons-logging', module: 'commons-logging' }
    implementation group: 'org.slf4j', name: 'jcl-over-slf4j'

    implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'

    //eessi pensjon libs
    implementation("no.nav.eessi.pensjon:ep-logging:1.0.28")
    implementation("no.nav.eessi.pensjon:ep-metrics:0.5.1")

    //GCP
    implementation("com.google.cloud:google-cloud-storage:$gcpStorageVersion")
    implementation(platform("com.google.cloud:libraries-bom:25.4.0"))

    testImplementation(platform("org.junit:junit-bom:5.8.2"))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.junit.jupiter:junit-jupiter-params")

    //mock - test
    testImplementation("org.mock-server:mockserver-client-java:5.13.2")
    testImplementation("org.mock-server:mockserver-netty:5.13.2") {
        exclude module: 'junit'
    }
    testImplementation "io.mockk:mockk:1.12.4"

    // Architecture tests
    testImplementation 'com.tngtech.archunit:archunit:0.23.1'

    testImplementation("com.ninja-squad:springmockk:3.1.1")
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude module: 'junit'
        exclude module: "mockito-core"
        exclude module: "junit-vintage-engine"
    }

    testImplementation 'org.springframework.kafka:spring-kafka-test:2.8.5'

    //testcontainers
    testImplementation("org.testcontainers:junit-jupiter:1.17.3")
    testImplementation "org.testcontainers:testcontainers:1.17.3"

}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

assert JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17): "Java 17 or newer is required"

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "17"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '17'
    }
}



repositories {

    mavenCentral()

    ["maven-release","ep-metrics"].each { repo ->
        String token = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key")
        if (!token) throw new NullPointerException("Missing token, you have to set GITHUB_TOKEN or gpr.key, see README")
        maven {
            url = uri("https://maven.pkg.github.com/navikt/$repo")
            credentials {
                username = "token"
                password = token
            }
        }
    }

}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

// https://docs.gradle.org/current/userguide/jacoco_plugin.html
jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

tasks.sonarqube.dependsOn tasks["jacocoTestReport"]

/* https://github.com/ben-manes/gradle-versions-plugin */
dependencyUpdates {
    rejectVersionIf {
        ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'pr']
                .any { qualifier -> it.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/ }
    }
    revision = 'release'
}
